plugins {
    id 'com.jfrog.bintray' version '1.7.1'
    id "com.github.hierynomus.license" version "0.13.1"
    id 'com.github.kt3k.coveralls' version '2.7.1'
    id 'com.github.ben-manes.versions' version '0.13.0'
}

configurations {
    testcode
}

dependencies {
    compile('xml-resolver:xml-resolver:1.2')
    compile('org.slf4j:slf4j-api:1.7.21')

    testcode sourceSets.test.output
}

tasks.bintrayUpload.dependsOn build

license {
    header rootProject.file('config/license_header.txt')

    // Toggle ignoreFailures to false to enable license-checking that will fail the build
    ignoreFailures true

    ext.name = 'David Clark'
    ext.year = Calendar.instance.get(Calendar.YEAR)

    excludes(['**/*.txt', '**/*.dtd', '**/*.xml', '**/*.ent', '**/*.jks'])
}

bintray {
    def bintrayUser = project.hasProperty('user') ? project.property('user') : null;
    def bintrayKey = project.hasProperty('key') ? project.property('key') : null;

    if (bintrayUser && bintrayKey) {
        user = bintrayUser
        key = bintrayKey

        publications = ['mavenJava']

        pkg {
            repo = 'dclark'
            name = 'http-builder-ng-core'
            userOrg = 'http-builder-ng'
            desc = 'Groovy client for making http requests'
            licenses = ['Apache-2.0']
            labels = ['groovy']
            vcsUrl = 'https://github.com/http-builder-ng/http-builder-ng'
        }
    }
}
